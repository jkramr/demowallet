plugins {
	id 'java'
	id 'idea'
	id "com.google.protobuf" version "0.8.8"
}

group = 'com.jkramr'
version = '0.0.1'
sourceCompatibility = '12'

def grpcVersion = '1.20.0'
def protoVersion = '3.7.1'

repositories {
	mavenCentral()
}

dependencies {
	compile("io.grpc:grpc-netty:${grpcVersion}")
	compile("io.grpc:grpc-protobuf:${grpcVersion}")
	compile("io.grpc:grpc-stub:${grpcVersion}")
	compile("javax.annotation:javax.annotation-api:1.3.2") //workaround: https://github.com/grpc/grpc-java/issues/5343
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:${protoVersion}"
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
		}
	}
	generateProtoTasks {
		all().each { task ->
			task.builtins {
				java { }
			}
		}
		all()*.plugins {
			grpc {}
		}
	}
	generatedFilesBaseDir = "$projectDir/src"
}

sourceSets {
	main {
		proto {
			srcDir "../demowallet-proto"
		}
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}